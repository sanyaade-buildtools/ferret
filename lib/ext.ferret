-- core.ferret
--

in Ext use Core ;

-- semi-common stack shuffles
: rot ( a b c -- b c a ) push swap pop swap ;
: -rot ( a b c -- c a b ) swap push swap pop ;

-- assertion
: ? ( bool -- ) not if "assertion failed" abort then ;

-- equality function
: is ( x -- xt ) with x -> {x =} ;

-- optionally execute a block
: maybe ( bool xt -- j*x bool ) swap if apply T else drop F then ;

-- try one branch, if fails, do another
: either ( xt xt -- j*x ) push try if pop drop else pop apply then ;

-- currying
: curry ( x xt -- xt ) with x block -> {x block apply} ;

-- composing blocks
: compose ( xt xt -- xt ) with a b -> {a apply b apply} ;

-- flip the arguments to a function
: flip ( xt -- xt ) {swap} swap compose ;

-- logical inverse the result of a function
: inverse ( xt -- xt ) {not} compose ;

-- type predicate
: type? ( t -- xt ) {swap type = ?} curry ;

-- type predicateas
: atom? ( x -- ) type Atom = ? ;
: block? ( x -- ) type Block = ? ;
: bool? ( x -- ) type Bool = ? ;
: char? ( x -- ) type Char = ? ;
: file? ( x -- ) type File = ? ;
: float? ( x -- ) type Float = ? ;
: int? ( x -- ) type Int = ? ;
: list? ( x -- ) type List = ? ;
: pid? ( x -- ) type Pid = ? ;
: string? ( x -- ) type String = ? ;
: url? ( x -- ) type Url = ? ;

-- apply a block n times
: times ( xt n -- j*x ) swap {for get lift next} do drop ;

-- print anything to stdout
: . ( x -- ) form puts ;

-- ordering of the top two cells
: ord< ( a b -- a b | b a ) over over < if swap then ;
: ord> ( a b -- a b | b a ) over over > if swap then ;
