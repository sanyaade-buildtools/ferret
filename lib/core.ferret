;; core.ferret
;;

;; identity function
id: fn [x] [x]

;; constant function
const: fn [x] [fn [_] [x]]

;; the ignore function
ignore: const none

;; equality function
is: fn [x] [fn [i] [i = x]]

;; generic predicate
type?: fn [t] [fn [x] [t = type x]]

;; type predicates
none?: type? 'none!
array?: type? 'array!
quote?: type? 'quote!
word?: type? 'word!
var?: type? 'var!
proc?: type? 'proc!
num?: type? 'num!
string?: type? 'string!
char?: type? 'char!
boolean?: type? 'boolean!
block?: type? 'block!
paren?: type? 'paren!
pid?: type? 'pid!
socket?: type? 'socket!

;; create a simple function with no arguments
does: fn [block] [fn [] block]

;; conditional apply on true
when: fn [test block] [
  if test [do block] []
]

;; conditional apply on false
unless: fn [test block] [
  if test [] [do block]
]

;; execute a block n times
times: fn [n block] [
  for i n [do block]
]

;; profile a block of code
time: fn [block] [
  let [now: clock] [
    do block
    print "Time:" && (form clock - now) & "s"
  ]
]
